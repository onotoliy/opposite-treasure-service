buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:2.3.6.RELEASE'
    }
}

apply plugin: 'pmd'
apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'checkstyle'
apply plugin: 'maven-publish'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group 'com.github.onotoliy.opposite'
version '1.0.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
    maven {
        url "https://plugins.gradle.org/m2/"
    }
    maven {
        url "http://185.12.95.242:8088/repository/onotoliy-maven-group/"
        credentials {
            username = property('onotoliy.nexus.username')
            password = property('onotoliy.nexus.password')
        }
    }
}

dependencies {

    implementation 'com.github.onotoliy.opposite:treasure-data-jvm:1.0.15'

    /**
     * Push Notification
     */
    implementation 'com.google.firebase:firebase-admin:7.0.0'
    implementation 'com.twilio.sdk:twilio:7.20.0'

    /**
     * Spring Boot
     */
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-jooq'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    implementation 'org.springframework.boot:spring-boot-starter-activemq'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    /**
     * Keycloak
     */
    implementation 'org.keycloak:keycloak-spring-boot-starter:4.8.3.Final'
    implementation 'org.keycloak:keycloak-admin-client:4.8.3.Final'
    implementation 'org.jboss.resteasy:resteasy-client:3.6.1.Final'
    implementation 'org.jboss.resteasy:resteasy-jackson2-provider:3.6.1.Final'

    /**
     * Kotlin
     */
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-runtime:0.11.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-runtime-common:0.11.0'

    /**
     * JOOQ.
     */
    implementation 'org.jooq:jooq:3.11.11'
    implementation 'org.jooq:jooq-meta:3.11.11'
    implementation 'org.jooq:jooq-codegen:3.11.11'
    implementation 'org.liquibase:liquibase-core'
    implementation 'org.testcontainers:postgresql:1.11.3'

    /**
     * JDBC Driver PosgreSQL
     */
    runtimeOnly 'org.postgresql:postgresql'

    /**
     * Тестирование.
     */
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

compileJava.dependsOn(processResources)

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Checkstyle) {
    exclude '**/com/github/onotoliy/opposite/treasure/jooq/**'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

javadoc {
    options.encoding = 'UTF-8'
    exclude 'com/github/onotoliy/opposite/treasure/jooq/**'
}

checkstyle {
    toolVersion '7.8.1'
    configFile file('checkstyle.xml')
}

checkstyleMain {
    source = sourceSets.main.allJava
}

checkstyleTest {
    source = sourceSets.main.allJava
}

pmd {
    rulePriority 1
    ruleSetFiles file("pmd.xml")
}

pmdMain {
    source = sourceSets.main.allJava
}

pmdTest {
    source = sourceSets.main.allJava
}

jacoco {
    toolVersion = "0.8.4"
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.enabled true
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'BUNDLE'
            limit {
                counter = 'INSTRUCTION'
                value = 'COVEREDRATIO'
                minimum = 0.0
            }
        }
    }
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

check.dependsOn jacocoTestCoverageVerification

publishing {
    repositories {
        maven {
            url = 'http://185.12.95.242:8088/repository/maven-releases/'
            credentials {
                username = property('onotoliy.nexus.username')
                password = property('onotoliy.nexus.password')
            }
        }
    }

    publications {
        maven(MavenPublication) {
            artifact("build/libs/treasure-service-$version\\.jar") {
                extension "jar"
            }
        }
    }
}

task jooqGen(dependsOn: 'classes', type: JavaExec) {
    main = 'com.github.onotoliy.opposite.treasure.JooqGenerator'
    classpath = sourceSets.main.runtimeClasspath
    args '/liquibase/db.changelog-master.xml', 'src/main/java', 'com.github.onotoliy.opposite.treasure.jooq'
}